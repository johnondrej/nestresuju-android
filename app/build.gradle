apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "cz.nestresuju"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 5
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    viewBinding.enabled = true

    signingConfigs {
        def keystoreProperties = new Properties()
        def keystorePropertiesFile = file("${project.rootDir}\\keystore.properties")
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        release {
            storeFile file("${project.rootDir}\\keystore\\${keystoreProperties['keystore_file']}")
            storePassword keystoreProperties['keystore_password']
            keyAlias keystoreProperties['key_alias']
            keyPassword keystoreProperties['key_password']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    crashlyticsCollectionEnabled: "false",
                    analyticsDisabled           : "true"
            ]
            ext.enableCrashlytics = false

            firebaseAppDistribution {
                groups = "Nestresuju"
            }
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    crashlyticsCollectionEnabled: "true",
                    analyticsDisabled           : "false"
            ]
            signingConfig signingConfigs.release

            firebaseAppDistribution {
                groups = "Nestresuju"
            }
        }
    }

    flavorDimensions "api"

    productFlavors {

        prodApi {
            dimension "api"
        }

        stageApi {
            dimension "api"
            applicationIdSuffix ".stage"
            versionNameSuffix "-stage"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-rc01'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.room:room-ktx:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'

    // Coil
    implementation "io.coil-kt:coil:0.10.0"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'

    // Epoxy
    implementation 'com.airbnb.android:epoxy:3.9.0'
    kapt 'com.airbnb.android:epoxy-processor:3.9.0'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:18.0.0'
    implementation 'com.google.firebase:firebase-messaging:21.0.1'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    // Koin
    implementation 'org.koin:koin-core:2.1.3'
    implementation 'org.koin:koin-androidx-scope:2.1.3'
    implementation 'org.koin:koin-androidx-viewmodel:2.1.3'

    // Live Event
    implementation 'com.github.hadilq.liveevent:liveevent:1.2.0'

    // Material Rating Bar
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'

    // Moshi
    implementation 'com.squareup.moshi:moshi:1.9.2'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.2'

    // OAuth handling
    implementation 'cz.ackee.ackroutine:coroutine-adapter:1.0.1'
    implementation 'cz.ackee.ackroutine:coroutine-oauth:1.0.1'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.7.2'

    // ThreeTen
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // JUnit
    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

kapt {
    correctErrorTypes = true
}
